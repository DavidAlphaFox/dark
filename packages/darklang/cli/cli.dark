module Darklang =
  module Cli =
    type Command =
      | Help
      | RunScript of String * List<String>
      | RunFunction of fnName: String * args: List<String>
      | Show of String
      | Infer of String * String
      | Invalid of List<String>


    let showFn (name: String) (owner: String) (modules: List<String>) : Int64 =
      let fns =
        PACKAGE.Darklang.Stdlib.HttpClient.request
          "GET"
          "http://dark-packages.dlio.localhost:11003/functions/"
          []
          []

      let statusCode =
        fns
        |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.statusCode)
        |> Builtin.unwrap

      let parsedFns =
        if statusCode == 200L then
          fns
          |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.body)
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
          |> Builtin.Json.parse<List<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageFn.PackageFn>>
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.Option.Option.Some
        else
          PACKAGE.Darklang.Stdlib.Option.Option.None

      match parsedFns with
      | None ->
        Builtin.printLine "Error getting functions"
        1L
      | Some fn ->
        let packageSpecificFunction =
          (PACKAGE.Darklang.Stdlib.List.filter fn (fun f ->
            f.name.name
            == PACKAGE.Darklang.LanguageTools.ProgramTypes.FnName.Name.FnName(name)
            && f.name.owner == owner
            && f.name.modules == modules))
          |> PACKAGE.Darklang.Stdlib.List.head

        let result =
          match packageSpecificFunction with
          | Some f ->
            Builtin.printLine (
              PACKAGE.Darklang.PrettyPrinter.ProgramTypes.packageFn f
            )

            0L
          | None ->
            Builtin.printLine "Function not found"
            1L

        result

    let showType (name: String) (owner: String) (modules: List<String>) : Int64 =
      let types =
        PACKAGE.Darklang.Stdlib.HttpClient.request
          "GET"
          "http://dark-packages.dlio.localhost:11003/types/"
          []
          []

      let statusCode =
        types
        |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.statusCode)
        |> Builtin.unwrap

      let parsedTypes =
        if statusCode == 200L then
          types
          |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.body)
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
          |> Builtin.Json.parse<List<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageType>>
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.Option.Option.Some
        else
          PACKAGE.Darklang.Stdlib.Option.Option.None

      match parsedTypes with
      | None ->
        Builtin.printLine "Error getting types"
        1L
      | Some types ->
        let packageSpecificType =
          (PACKAGE.Darklang.Stdlib.List.filter types (fun t ->
            t.name.name == LanguageTools.ProgramTypes.TypeName.Name.TypeName(name)
            && t.name.owner == owner
            && t.name.modules == modules))
          |> PACKAGE.Darklang.Stdlib.List.head

        let result =
          match packageSpecificType with
          | Some t ->
            Builtin.printLine (
              PACKAGE.Darklang.PrettyPrinter.ProgramTypes.packageType t
            )

            0L
          | None ->
            Builtin.printLine "Type not found"
            1L

        result

    let showConstant (name: String) (owner: String) (modules: List<String>) : Int64 =
      let constants =
        PACKAGE.Darklang.Stdlib.HttpClient.request
          "GET"
          "http://dark-packages.dlio.localhost:11003/constants/"
          []
          []

      let statusCode =
        constants
        |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.statusCode)
        |> Builtin.unwrap

      let parsedConstants =
        if statusCode == 200L then
          constants
          |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.body)
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
          |> Builtin.Json.parse<List<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageConstant>>
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.Option.Option.Some
        else
          PACKAGE.Darklang.Stdlib.Option.Option.None

      match parsedConstants with
      | None ->
        Builtin.printLine "Error getting constants"
        1L
      | Some constants ->
        let packageSpecificConstant =
          (PACKAGE.Darklang.Stdlib.List.filter constants (fun c ->
            c.name.name
            == PACKAGE.Darklang.LanguageTools.ProgramTypes.ConstantName.Name.ConstantName
              name
            && c.name.owner == owner
            && c.name.modules == modules))
          |> PACKAGE.Darklang.Stdlib.List.head

        let result =
          match packageSpecificConstant with
          | Some c ->
            Builtin.printLine (
              PACKAGE.Darklang.PrettyPrinter.ProgramTypes.packageConstant c
            )

            0L

          | None ->
            Builtin.printLine "Constant not found"
            1L

        result

    let showModule (name: String) : Int64 =
      let modules =
        PACKAGE.Darklang.Stdlib.HttpClient.request
          "GET"
          ("http://dark-packages.dlio.localhost:11003/modules/" ++ name)
          []
          []

      let statusCode =
        modules
        |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.statusCode)
        |> Builtin.unwrap

      let parsedModules =
        if statusCode == 200L then
          modules
          |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.body)
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
          |> Builtin.Json.parse<PACKAGE.Darklang.Stdlib.Packages>
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.Option.Option.Some
        else
          PACKAGE.Darklang.Stdlib.Option.Option.None


      match parsedModules with
      | None ->
        Builtin.printLine "Error getting package modules"
        1L
      | Some modules ->
        if
          modules
          == PACKAGE.Darklang.Stdlib.Packages
            { types = []; fns = []; constants = [] }
        then
          Builtin.printLine "module not found"
          1L
        else
          let result = modules |> PACKAGE.Darklang.PrettyPrinter.packages
          Builtin.printLine result
          0

    let showPackage (owner: String) : Int64 =
      let packages =
        PACKAGE.Darklang.Stdlib.HttpClient.request
          "GET"
          $"http://dark-packages.dlio.localhost:11003/owner/{owner}"
          []
          []

      let statusCode =
        packages
        |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.statusCode)
        |> Builtin.unwrap

      let parsedPackages =
        if statusCode == 200L then
          packages
          |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.body)
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
          |> Builtin.printLine

          0L
        else
          Builtin.printLine "Error retrieving the package for the specified owner"
          1L

      parsedPackages


    let runFunction (fnName: String) (args: List<String>) : Int64 =
      match Builtin.Cli.executeFunction fnName args with
      | Ok result ->
        Builtin.printLine result
        0L
      | Error err ->
        Builtin.printLine err
        1L


    let generateCode (prompt: String) (scriptPath: String) : Int64 =

      let systemPrompt =
        (Builtin.File.read "canvases/dark-editor/system-prompt-cli.txt")
        |> Builtin.unwrap
        |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement

      let prompt = prompt ++ "\n" ++ systemPrompt

      let response = (PACKAGE.OpenAI.ChatCompletion.completion prompt)

      match response with
      | Error e ->
        Builtin.printLine e
        1L
      | Ok response ->
        Builtin.File.write
          (PACKAGE.Darklang.Stdlib.String.toBytes response)
          scriptPath

        Builtin.printLine $"Generated code saved to {scriptPath}"
        0L


    let parseArguments (args: List<String>) : Command =
      match args with
      | [ "-h" ] -> Command.Help
      | [ "--help" ] -> Command.Help
      | [ "--prompt"; prompt; "--script"; scriptPath ] ->
        Command.Infer(prompt, scriptPath)
      | [ "show"; fnName ] ->
        if Stdlib.String.startsWith fnName "@" then
          Command.Show fnName
        else
          Command.Invalid [ "Invalid package name" ]

      | opt :: args ->
        if PACKAGE.Darklang.Stdlib.String.startsWith opt "@" then
          let name = opt |> PACKAGE.Darklang.Stdlib.String.dropFirst_v0 1L
          Command.RunFunction name args
        else
          Command.RunScript(opt, args)

      | _ -> Command.Invalid [ "Invalid arguments" ]


    let executeCommand (command: Command) : Int64 =
      match command with
      | RunScript(scriptPath, args) ->
        match Builtin.File.read scriptPath with
        | Error e ->
          Builtin.printLine e
          1L
        | Ok script ->
          let script = PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement script

          match
            Builtin.Cli.parseAndExecuteScript
              scriptPath
              script
              (Dict { args = args })
          with
          | Ok exitCode -> exitCode
          | Error e ->
            Builtin.printLine (
              PACKAGE.Darklang.LanguageTools.RuntimeErrors.Error.toString e
            )

            1L


      | Help ->
        let helpText =
          """Usage: darklang [options] [SCRIPT]
Options:
  -h, --help                          Show this help message and exit
  show                                Show a package, function, type, constant, or module
  --prompt PROMPT  --script SCRIPT    Infer a script from the given prompt using OpenAI"""

        Builtin.printLine helpText
        0L


      | Show name ->

        let fullName = name |> PACKAGE.Darklang.Stdlib.String.dropFirst_v0 1L

        let owner =
          fullName
          |> PACKAGE.Darklang.Stdlib.String.split "."
          |> PACKAGE.Darklang.Stdlib.List.head
          |> Builtin.unwrap

        let modules =
          fullName
          |> PACKAGE.Darklang.Stdlib.String.split "."
          |> (PACKAGE.Darklang.Stdlib.List.drop 1L)
          |> PACKAGE.Darklang.Stdlib.List.dropLast

        let name =
          name
          |> PACKAGE.Darklang.Stdlib.String.dropFirst_v0 1L
          |> PACKAGE.Darklang.Stdlib.String.split "."
          |> PACKAGE.Darklang.Stdlib.List.last
          |> Builtin.unwrap

        let categoryRequest =
          PACKAGE.Darklang.Stdlib.HttpClient.request
            "GET"
            $"http://dark-packages.dlio.localhost:11003/category/{fullName}"
            []
            []

        let statusCode =
          categoryRequest
          |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.statusCode)
          |> Builtin.unwrap

        let category =
          if statusCode == 200L then
            categoryRequest
            |> PACKAGE.Darklang.Stdlib.Result.map (fun response -> response.body)
            |> Builtin.unwrap
            |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
          else
            "Error" |> PACKAGE.Darklang.Stdlib.String.toBytes

        match category with
        | "fn" -> PACKAGE.Darklang.Cli.showFn name owner modules
        | "type" -> PACKAGE.Darklang.Cli.showType name owner modules
        | "constant" -> PACKAGE.Darklang.Cli.showConstant name owner modules
        | _ ->
          let modules =
            fullName
            |> PACKAGE.Darklang.Stdlib.String.split "."
            |> (PACKAGE.Darklang.Stdlib.List.drop 1L)

          if PACKAGE.Darklang.Stdlib.List.length modules == 0L then
            PACKAGE.Darklang.Cli.showPackage owner
          else
            PACKAGE.Darklang.Cli.showModule fullName

      | RunFunction(fnName, args) -> PACKAGE.Darklang.Cli.runFunction fnName args

      | Infer(prompt, scriptPath) ->
        // let script = System.IO.File.ReadAllText scriptPath
        // Call OpenAI API with the prompt and script to generate a new script
        // let generatedScript = callOpenAI(prompt, script)
        // Execute the generated script here
        PACKAGE.Darklang.Cli.generateCode prompt scriptPath

      | Invalid args ->
        let args = args |> PACKAGE.Darklang.Stdlib.String.join " "
        Builtin.printLine $"Invalid command {args}. Use --help for more information."
        1L


    let executeCliCommand (args: List<String>) : Int64 =
      args
      |> PACKAGE.Darklang.Cli.parseArguments
      |> PACKAGE.Darklang.Cli.executeCommand