module Darklang =
  module LanguageTools =
    module LspServer =
      module DocSync =
        let hardcodedServerCapabilities
          ()
          : LanguageServerProtocol.Lifecycle.InitializeRequest.TextDocumentSyncServerCapabilities.TextDocumentSyncServerCapabilities =
          LanguageServerProtocol
            .Lifecycle
            .InitializeRequest
            .TextDocumentSyncServerCapabilities
            .TextDocumentSyncServerCapabilities
            .TextDocumentSyncOptions(
              LanguageServerProtocol.DocumentSync.TextDocument.TextDocumentSyncOptions.TextDocumentSyncOptions
                { openClose = Stdlib.Option.Option.Some true
                  change =
                    Stdlib.Option.Option.Some
                      LanguageServerProtocol.DocumentSync.TextDocument.TextDocumentSyncKind.TextDocumentSyncKind.Full
                  save =
                    Stdlib.Option.Option.Some(
                      LanguageServerProtocol
                        .DocumentSync
                        .TextDocument
                        .TextDocumentSyncOptions
                        .SaveOptionsOrBool
                        .SaveOptionsOrBool
                        .SaveOptions(
                          LanguageServerProtocol.DocumentSync.TextDocument.SaveOptions.SaveOptions
                            { includeText = Stdlib.Option.Option.Some true }
                        )
                    ) }
            )

        let parseAndReportDiagnostics
          (requestText: String)
          : Stdlib.Result.Result<WrittenTypes.ParsedFile, String> =
          let onMissing = LanguageTools.NameResolver.OnMissing.Allow
          let pm = LanguageTools.PackageManager.pm ()

          let parsed =
            requestText |> Parser.parseToSimplifiedTree |> Parser.parseFromTree

          let sourceFile =
            match parsed with
            | Ok parsedFile ->
              match parsedFile with
              | SourceFile sf -> sf
              | _ -> log "Invalid source file"
            | Error err -> log err

          let (_ptCliScriptModule, unresolvedNames) =
            Parser.CliScript.parse
              "LSP"
              "scriptName"
              onMissing
              pm
              "fileName"
              requestText

          Diagnostics.gatherAndReportDiagnostics
            sourceFile
            (unresolvedNames |> Stdlib.List.flatten)

          parsed

        // -- message-handling

        let handleTextDocumentDidOpen
          (state: LspState)
          (request:
            LanguageServerProtocol.DocumentSync.TextDocument.DidOpenTextDocumentNotification.DidOpenTextDocumentParams.DidOpenTextDocumentParams)
          : LspState =
          let parsed = parseAndReportDiagnostics request.textDocument.text

          { state with
              documentsInScope =
                Stdlib.Dict.set
                  state.documentsInScope
                  request.textDocument.uri
                  (LanguageTools.LspServer.DocumentInScope
                    { uri = request.textDocument.uri
                      text = request.textDocument.text
                      parsed = parsed }) }


        let handleTextDocumentDidChange
          (state: LspState)
          (request:
            LanguageServerProtocol.DocumentSync.TextDocument.DidChangeTextDocumentNotification.DidChangeTextDocumentParams.DidChangeTextDocumentParams)
          : LspState =
          let text = request.contentChanges |> Stdlib.List.head |> Builtin.unwrap

          { state with
              documentsInScope =
                Stdlib.Dict.set
                  state.documentsInScope
                  request.textDocument.uri
                  (LanguageTools.LspServer.DocumentInScope
                    { uri = request.textDocument.uri
                      text = text.text
                      parsed = parseAndReportDiagnostics text.text }) }


        let handleTextDocumentDidSave
          (state: LspState)
          (request:
            LanguageServerProtocol.DocumentSync.TextDocument.DidSaveTextDocumentNotification.DidSaveTextDocumentParams.DidSaveTextDocumentParams)
          : LspState =
          match request.text with
          | None ->
            log
              "WARNING: no text in didSave, even though we configured to include text in these requests, in the `initialize` handshake"

            state

          | Some text ->
            let parsed = parseAndReportDiagnostics text

            { state with
                documentsInScope =
                  Stdlib.Dict.set
                    state.documentsInScope
                    request.textDocument.uri
                    (LanguageTools.LspServer.DocumentInScope
                      { uri = request.textDocument.uri
                        text = text
                        parsed = parsed }) }


        let handleTextDocumentDidClose
          (state: LspState)
          (requestParams:
            LanguageServerProtocol.DocumentSync.TextDocument.DidCloseTextDocumentNotification.DidCloseTextDocumentParams.DidCloseTextDocumentParams)
          : LspState =
          { state with
              documentsInScope =
                Stdlib.Dict.remove
                  state.documentsInScope
                  requestParams.textDocument.uri }