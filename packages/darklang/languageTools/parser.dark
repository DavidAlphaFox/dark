module Darklang =
  module LanguageTools =
    module Parser =
      type Point = { row: Int64; column: Int64 }

      type Range = { start: Point; end_: Point }

      type ParsedNode =
        {
          // e.g., a node of `typ` `let_epression` has a child node with a `body` field name
          fieldName: Stdlib.Option.Option<String>

          /// e.g. `source_file`, `fn_def`, `expression`, `let_expression`
          typ: String

          /// The text of this node as it was in the unparsed source code
          text: String

          /// Where in the source code is this node written/contained
          /// i.e. Line 1, Column 2 to Line 1, Column 5
          sourceRange: Range

          children: List<ParsedNode>
        }


      let parse (text: String) : ParsedNode =
        Builtins.LanguageTools.Parser.parse text

      // --------------------
      // Helper functions
      // --------------------

      let getText (node: ParsedNode) : String = node.text

      let findNodeByFieldName
        (node: ParsedNode)
        (fieldName: String)
        : PACKAGE.Darklang.Stdlib.Option.Option<ParsedNode> =
        let children = node.children

        let filteredChildren =
          children
          |> PACKAGE.Darklang.Stdlib.List.filter (fun c ->
            match c.fieldName with
            | Some fName -> fName == fieldName
            | None -> false)

        match filteredChildren with
        | [ c ] -> PACKAGE.Darklang.Stdlib.Option.Option.Some c
        | [] -> PACKAGE.Darklang.Stdlib.Option.Option.None
        | _ -> PACKAGE.Darklang.Stdlib.Option.Option.None // TODO: this should error, there are multiple matches

      let stringToPackage
        (str: String)
        : PACKAGE.Darklang.LanguageTools.ProgramTypes.FQName.Package =
        let name = PACKAGE.Darklang.LanguageTools.ProgramTypes.FnName.Name.FnName str

        PACKAGE.Darklang.LanguageTools.ProgramTypes.FQName.Package
          { owner = "TODO"
            modules = [ "TODO" ]
            name = name
            version = 0L }


      // --------------------
      // Parsing to WrittenTypes
      // --------------------

      // TODO: support more types
      type WTTypeReference =
        | TInt64
        | TBool
        | TString

      let parseTypeReference
        (node: ParsedNode)
        : PACKAGE.Darklang.Stdlib.Option.Option<WTTypeReference> =
        // TODO: support more types
        let typ = node.typ

        if typ == "type" then
          match (getText node) with
          | "Int" ->
            PACKAGE.Darklang.Stdlib.Option.Option.Some
              PACKAGE.Darklang.LanguageTools.WrittenTypes.TypeReference.TInt64
          | "Int64" ->
            PACKAGE.Darklang.Stdlib.Option.Option.Some
              PACKAGE.Darklang.LanguageTools.WrittenTypes.TypeReference.TInt64
          | "Bool" ->
            PACKAGE.Darklang.Stdlib.Option.Option.Some
              PACKAGE.Darklang.LanguageTools.WrittenTypes.TypeReference.TBool
          | "String" ->
            PACKAGE.Darklang.Stdlib.Option.Option.Some
              PACKAGE.Darklang.LanguageTools.WrittenTypes.TypeReference.TString
          | _ -> PACKAGE.Darklang.Stdlib.Option.Option.None
        else
          PACKAGE.Darklang.Stdlib.Option.Option.None // this should return and error?


      let parseExprCase
        (parsed: ParsedNode)
        : PACKAGE.Darklang.Stdlib.Result.Result<PACKAGE.Darklang.LanguageTools.WrittenTypes.Expr, String> =
        match parsed.typ with
        | "identifier" ->
          PACKAGE.Darklang.Stdlib.Result.Result.Ok(
            PACKAGE.Darklang.LanguageTools.WrittenTypes.Expr.EVariable(0L, getText parsed)
          )
        | "infix_operation" ->
          let left =
            (findNodeByFieldName parsed "left")
            |> Builtin.unwrap
            |> parseExpr
            |> Builtin.unwrap

          let right =
            (findNodeByFieldName parsed "right")
            |> Builtin.unwrap
            |> parseExpr
            |> Builtin.unwrap

          let operatorNode = (findNodeByFieldName parsed "operator") |> Builtin.unwrap
          let operatorText = getText operatorNode

          let operator =
            match operatorText with
            | "+" ->
              PACKAGE.Darklang.LanguageTools.WrittenTypes.Infix.InfixFnCall(
                PACKAGE.Darklang.LanguageTools.WrittenTypes.InfixFnName.ArithmeticPlus
              )
            | "-" ->
              PACKAGE.Darklang.LanguageTools.WrittenTypes.Infix.InfixFnCall(
                PACKAGE.Darklang.LanguageTools.WrittenTypes.InfixFnName.ArithmeticMinus
              )
            | _ ->
              PACKAGE.Darklang.Stdlib.Result.Result.Error "TODO : operator not implemented"

          PACKAGE.Darklang.Stdlib.Result.Result.Ok(
            PACKAGE.Darklang.LanguageTools.WrittenTypes.Expr.EInfix(
              0L,
              operator,
              left,
              right
            )
          )

        | "let_expression" ->
          let identifier =
            (findNodeByFieldName parsed "identifier") |> Builtin.unwrap |> getText

          let expr =
            (findNodeByFieldName parsed "expr")
            |> Builtin.unwrap
            |> parseExpr
            |> Builtin.unwrap

          let body =
            (findNodeByFieldName parsed "body")
            |> Builtin.unwrap
            |> parseExpr
            |> Builtin.unwrap

          PACKAGE.Darklang.Stdlib.Result.Result.Ok(
            PACKAGE.Darklang.LanguageTools.WrittenTypes.Expr.ELet(
              0L,
              PACKAGE.Darklang.LanguageTools.WrittenTypes.LetPattern.LPVariable(
                0L,
                identifier
              ),
              expr,
              body
            )
          )

        // TODO: This is not tested yet
        | "function_call" ->
          let fnName = (findNodeByFieldName parsed "fn") |> Builtin.unwrap |> getText

          let args =
            (findNodeByFieldName parsed "args")
            |> Builtin.unwrap
            |> PACKAGE.Darklang.Stdlib.List.map (
              fun arg -> (parseExpr arg)
              |> Builtin.unwrap
            )

          let fnCallExpr =
            PACKAGE.Darklang.LanguageTools.WrittenTypes.Expr.EApply(
              0L,
              PACKAGE.Darklang.LanguageTools.WrittenTypes.Expr.EFnName(
                0L,
                PACKAGE.Darklang.LanguageTools.WrittenTypes.Name.Unresolved
                  [ "Darklang"; "Stdlib"; "Int"; fnName ]
              ),
              [],
              args
            )

          PACKAGE.Darklang.Stdlib.Result.Result.Ok(fnCallExpr)
        | _ ->
          PACKAGE.Darklang.Stdlib.Result.Result.Error
            "TODO : parseExprCase not implemented"


      let parseExpr
        (parsed: ParsedNode)
        : PACKAGE.Darklang.Stdlib.Result.Result<PACKAGE.Darklang.LanguageTools.WrittenTypes.Expr, String> =
        if parsed.typ == "expression" then
          match parsed.children with
          | [ single ] -> parseExprCase single
          | _ -> PACKAGE.Darklang.Stdlib.Result.Result.Error "Not a single expression"
        else
          PACKAGE.Darklang.Stdlib.Result.Result.Error "Not an expression"


      let parseNodeToWT
        (parsed: ParsedNode)
        : PACKAGE.Darklang.Stdlib.Result.Result<List<PACKAGE.Darklang.LanguageTools.WrittenTypes.PackageFn.PackageFn>, String> =
        if parsed.typ == "source_file" then
          match parsed.children with
          | [ fnDef ] -> parsePackageFn fnDef
          | _ -> PACKAGE.Darklang.Stdlib.Result.Result.Error "Not a single fn_def"
        else
          PACKAGE.Darklang.Stdlib.Result.Result.Error "Not a source_file"


      let parsePackageFn
        (parsed: ParsedNode)
        : PACKAGE.Darklang.Stdlib.Result.Result<List<PACKAGE.Darklang.LanguageTools.WrittenTypes.PackageFn.PackageFn>, String> =
        if parsed.typ == "fn_def" then
          let parameters =
            match (findNodeByFieldName parsed "params") with
            | Some p -> (parseFnParams p) |> Builtin.unwrap
            | None -> PACKAGE.Darklang.Stdlib.Result.Result.Error "No params"

          let body =
            (findNodeByFieldName parsed "body")
            |> Builtin.unwrap
            |> parseExpr
            |> Builtin.unwrap

          let returnType =
            (findNodeByFieldName parsed "return_type")
            |> Builtin.unwrap
            |> parseTypeReference
            |> Builtin.unwrap

          let name =
            ((findNodeByFieldName parsed "name") |> Builtin.unwrap |> getText)
            |> stringToPackage

          PACKAGE.Darklang.Stdlib.Result.Result.Ok
            [ PACKAGE.Darklang.LanguageTools.WrittenTypes.PackageFn.PackageFn
                { name = name
                  body = body
                  typeParams = []
                  parameters = parameters
                  returnType = returnType
                  description = "" } ]
        else
          PACKAGE.Darklang.Stdlib.Result.Result.Error parsed


      let parseFnParams
        (parsed: ParsedNode)
        : PACKAGE.Darklang.Stdlib.Result.Result<List<PACKAGE.Darklang.LanguageTools.WrittenTypes.PackageFn.Parameter>, String> =
        if parsed.typ == "fn_params_def" then
          let parameters =
            parsed.children
            |> PACKAGE.Darklang.Stdlib.List.map (fun p ->
              (parseFnParam p) |> Builtin.unwrap)

          PACKAGE.Darklang.Stdlib.Result.Result.Ok parameters
        else
          PACKAGE.Darklang.Stdlib.Result.Result.Error "Not a fn_params_def"


      let parseFnParam
        (parsed: ParsedNode)
        : PACKAGE.Darklang.Stdlib.Result.Result<PACKAGE.Darklang.LanguageTools.WrittenTypes.PackageFn.Parameter, String> =
        if parsed.typ == "fn_param_def" then
          PACKAGE.Darklang.Stdlib.Result.Result.Ok(
            PACKAGE.Darklang.LanguageTools.WrittenTypes.PackageFn.Parameter
              { name =
                  (findNodeByFieldName parsed "identifier") |> Builtin.unwrap |> getText
                typ =
                  (findNodeByFieldName parsed "typ")
                  |> Builtin.unwrap
                  |> parseTypeReference
                  |> Builtin.unwrap
                description = "" }
          )
        else
          PACKAGE.Darklang.Stdlib.Result.Result.Error "Not a fn_param_def"