module Darklang =
  module LanguageTools =
    module NameResolver =

      type NameResolutionSettings = { allowError: Bool }


      let empty () : NameResolver.NameResolutionSettings =
        NameResolver.NameResolutionSettings { allowError = true }

      let namesToTry
        (given: List<String>)
        (modules: List<String>)
        : List<List<String>> =
        match modules with
        | [] -> [ given ]
        | _ ->
          let rest = Stdlib.List.dropLast modules
          let current = Stdlib.List.append modules given
          Stdlib.List.append [ current ] (namesToTry given rest)


      // TODO: rename this
      let resolveNameTypeName
        (parser: String -> Stdlib.Result.Result<String>)
        (names: List<String>)
        (packageThingExists: String -> Bool)
        (builtinThingExists: String -> Bool)
        (nameErrorType: LanguageTools.RuntimeErrors.NameResolution.NameType)
        : LanguageTools.ProgramTypes.NameResolution<LanguageTools.ProgramTypes.FQTypeName.FQTypeName> =
        let modules = Stdlib.List.dropLast names
        let name = Stdlib.List.last names

        match name with
        | None ->
          Stdlib.Result.Result.Error(
            LanguageTools.RuntimeErrors.NameResolution.Error
              { errorType =
                  LanguageTools.RuntimeErrors.NameResolution.ErrorType.InvalidPackageName
                nameType = nameErrorType
                names = names }
          )
        | Some name ->
          match modules with
          | "PACKAGE" :: owner :: modules ->

            let name = (parser name) |> Builtin.unwrap

            let fqName =
              [ owner ]
              |> Stdlib.List.append modules
              |> Stdlib.List.append [ name ]
              |> Stdlib.String.join "."

            let packageThingExists = packageThingExists fqName

            if packageThingExists then
              Stdlib.Result.Result.Ok(
                (ProgramTypes.FQTypeName.Package
                  { owner = owner
                    modules = modules
                    name =  name
                    version = 0L })
                |> ProgramTypes.FQTypeName.FQTypeName.Package
              )
            else
              Stdlib.Result.Result.Error(
                LanguageTools.RuntimeErrors.NameResolution.Error
                  { errorType =
                      LanguageTools.RuntimeErrors.NameResolution.ErrorType.NotFound
                    nameType = nameErrorType
                    names = Stdlib.List.append modules [ name ] }
              )

          | modules ->
            // TODO : check if user program exists
            Stdlib.Result.Result.Ok(
              (ProgramTypes.FQTypeName.UserProgram
                { modules = modules
                  name = name
                  version = 0L })
              |> ProgramTypes.FQTypeName.FQTypeName.UserProgram
            )




      let resolveTypeName
        (parser: String -> Stdlib.Result.Result<String>)
        (nameErrorType: LanguageTools.RuntimeErrors.NameResolution.NameType)
        (packageThingExists: String -> Bool)
        (builtinThingExists: String -> Bool)
        (allowError: Bool)
        (currentModule: List<String>)
        (name: LanguageTools.WrittenTypesToProgramTypes.Name)
        : LanguageTools.ProgramTypes.NameResolution<LanguageTools.ProgramTypes.FQTypeName.FQTypeName> =

        match name with
        | Unresolved given ->
          let result =
            Stdlib.List.fold
              (NameResolver.namesToTry given currentModule)

              (Stdlib.Result.Result.Error(
                LanguageTools.RuntimeErrors.NameResolution.Error
                  { nameType = nameErrorType
                    errorType =
                      LanguageTools.RuntimeErrors.NameResolution.ErrorType.NotFound
                    names = given }
              ))

              (fun currentResult pathToTry ->
                match currentResult with
                | Ok _ -> currentResult
                | Error _ ->
                  let newResult =
                    resolveNameTypeName
                      parser
                      pathToTry
                      packageThingExists
                      builtinThingExists
                      nameErrorType

                  match newResult with
                  | Ok _ -> newResult
                  | Error _ -> currentResult)

          match result with
          | Ok result -> Stdlib.Result.Result.Ok result
          | Error err ->
            if allowError then
              Stdlib.Result.Result.Error err
            else
              Stdlib.Result.Result.Error "TODO: Raise exception"


        | _ -> Stdlib.Result.Result.Error "TODO : builtin and user program"



      module TypeName =
        let packageTypeExists (typeName: String) : Bool =
          (LanguageTools.PackageManager.Type.byName typeName) |> Stdlib.Result.isOk

        // We don't currently have any Builtin types, and don't expect to.
        let builtinThingExists (typeName: String) : Bool = false


        let maybeResolve
          (resolver: NameResolver.NameResolutionSettings)
          (currentModule: List<String>)
          (name: LanguageTools.WrittenTypesToProgramTypes.Name)
          : LanguageTools.ProgramTypes.NameResolution<LanguageTools.ProgramTypes.FQTypeName.FQTypeName> =

          NameResolver.resolveTypeName
            LanguageTools.Parser.parseName
            LanguageTools.RuntimeErrors.NameResolution.NameType.Type
            TypeName.packageTypeExists
            TypeName.builtinThingExists
            currentModule
            true
            name

        let resolve
          (resolver: NameResolver.NameResolutionSettings)
          (currentModule: List<String>)
          (name: LanguageTools.WrittenTypesToProgramTypes.Name)
          : LanguageTools.ProgramTypes.NameResolution<LanguageTools.ProgramTypes.FQTypeName.FQTypeName> =
          NameResolver.resolveTypeName
            LanguageTools.Parser.parseName
            LanguageTools.RuntimeErrors.NameResolution.NameType.Type
            TypeName.packageTypeExists
            TypeName.builtinThingExists
            resolver.allowError
            currentModule
            name



      // helper fuctions for the resolve function
      // TODO: rename this
      let resolveNameFnName
        (parser: String -> Stdlib.Result.Result<String>)
        (names: List<String>)
        (packageThingExists: String -> Bool)
        (builtinThingExists: String -> Bool)
        (nameErrorType: LanguageTools.RuntimeErrors.NameResolution.NameType)
        : LanguageTools.ProgramTypes.NameResolution<LanguageTools.ProgramTypes.FQFnName.FQFnName> =
        let modules = Stdlib.List.dropLast names
        let name = Stdlib.List.last names

        match name with
        | None ->
          Stdlib.Result.Result.Error(
            LanguageTools.RuntimeErrors.NameResolution.Error
              { errorType =
                  LanguageTools.RuntimeErrors.NameResolution.ErrorType.InvalidPackageName
                nameType = nameErrorType
                names = names }
          )
        | Some name ->
          match modules with
          | "PACKAGE" :: owner :: modules ->

            let name = (parser name) |> Builtin.unwrap

            let fqName =
              [ owner ]
              |> Stdlib.List.append modules
              |> Stdlib.List.append [ name ]
              |> Stdlib.String.join "."

            let packageThingExists = packageThingExists fqName

            if packageThingExists then
              Stdlib.Result.Result.Ok(
                (ProgramTypes.FQFnName.Package
                  { owner = owner
                    modules = modules
                    name =  name
                    version = 0L })
                |> ProgramTypes.FQFnName.FQFnName.Package
              )
            else
              Stdlib.Result.Result.Error(
                LanguageTools.RuntimeErrors.NameResolution.Error
                  { errorType =
                      LanguageTools.RuntimeErrors.NameResolution.ErrorType.NotFound
                    nameType = nameErrorType
                    names = Stdlib.List.append modules [ name ] }
              )

          | "Builtin" :: modules ->
            let builtinName =
              Stdlib.String.join (Stdlib.List.append modules [ name ]) "."

            let builtinExists = builtinThingExists builtinName

            if builtinExists then
              let builtIn =
                (ProgramTypes.FQFnName.Builtin
                  { modules = modules
                    name = name
                    version = 0L })

              Stdlib.Result.Result.Ok(builtIn |> ProgramTypes.FQFnName.FQFnName.Builtin)

            else
              Stdlib.Result.Result.Error(
                LanguageTools.RuntimeErrors.NameResolution.Error
                  { errorType =
                      LanguageTools.RuntimeErrors.NameResolution.ErrorType.NotFound
                    nameType = nameErrorType
                    names = Stdlib.List.append modules [ name ] }
              )

          | modules ->
            // TODO : check if user program exists
            Stdlib.Result.Result.Ok(
              (ProgramTypes.FQFnName.UserProgram
                { modules = modules
                  name = name
                  version = 0L })
              |> ProgramTypes.FQFnName.FQFnName.UserProgram
            )


      let resolveFnName
        (parser: String -> Stdlib.Result.Result<String>)
        (nameErrorType: LanguageTools.RuntimeErrors.NameResolution.NameType)
        (packageThingExists: String -> Bool)
        (builtinThingExists: String -> Bool)
        (allowError: Bool)
        (currentModule: List<String>)
        (name: LanguageTools.WrittenTypesToProgramTypes.Name)
        : LanguageTools.ProgramTypes.NameResolution<LanguageTools.ProgramTypes.FQFnName.FQFnName> =

        match name with
        | Unresolved given ->
          let result =
            Stdlib.List.fold
              (NameResolver.namesToTry given currentModule)

              (Stdlib.Result.Result.Error(
                LanguageTools.RuntimeErrors.NameResolution.Error
                  { nameType = nameErrorType
                    errorType =
                      LanguageTools.RuntimeErrors.NameResolution.ErrorType.NotFound
                    names = given }
              ))

              (fun currentResult pathToTry ->
                match currentResult with
                | Ok _ -> currentResult
                | Error _ ->
                  let newResult =
                    resolveNameFnName
                      parser
                      pathToTry
                      packageThingExists
                      builtinThingExists
                      nameErrorType

                  match newResult with
                  | Ok _ -> newResult
                  | Error _ -> currentResult)

          match result with
          | Ok result -> Stdlib.Result.Result.Ok result
          | Error err ->
            if allowError then
              Stdlib.Result.Result.Error err
            else
              Stdlib.Result.Result.Error "TODO: Raise exception"


        | _ -> Stdlib.Result.Result.Error "TODO : builtin and user program"

      module FnName =
        let packageFnExists (fnName: String) : Bool =
          (LanguageTools.PackageManager.Function.byName fnName) |> Stdlib.Result.isOk


        let builtinThingExists (fnName: String) : Bool =
          (Builtin.LanguageTools.allBuiltinFns ())
          |> Stdlib.List.findFirst (fun f -> f.name == fnName)
          |> Stdlib.Option.isSome


        let maybeResolve
          (resolver: NameResolver.NameResolutionSettings)
          (currentModule: List<String>)
          (name: LanguageTools.WrittenTypesToProgramTypes.Name)
          : LanguageTools.ProgramTypes.NameResolution<LanguageTools.ProgramTypes.FQFnName.FQFnName> =

          NameResolver.resolveFnName
            LanguageTools.Parser.parseName
            LanguageTools.RuntimeErrors.NameResolution.NameType.Function
            FnName.packageFnExists
            FnName.builtinThingExists
            currentModule
            true
            name

        let resolve
          (resolver: NameResolver.NameResolutionSettings)
          (currentModule: List<String>)
          (name: LanguageTools.WrittenTypesToProgramTypes.Name)
          : LanguageTools.ProgramTypes.NameResolution<LanguageTools.ProgramTypes.FQFnName.FQFnName> =

          NameResolver.resolveFnName
            LanguageTools.Parser.parseName
            LanguageTools.RuntimeErrors.NameResolution.NameType.Function
            FnName.packageFnExists
            FnName.builtinThingExists
            resolver.allowError
            currentModule
            name